prototype(Wysiwyg.CookieHandling:Content.CookieLayer) < prototype(Neos.Fusion:Component) {

    /**
        PROPS:
         - modalId
         - consentCookieName
         - translationPackage
         - translationLocale
         - privacyStatementPageUri
         - legalNodesPageUri
         - cookieLayerDisabled
         - showCookieDetails
    */

    // Layer-Attributes
    modalId = 'modal-cookie'
    consentCookieName = ${Configuration.setting('Wysiwyg.CookieHandling.cookieGroups.technical.cookies.cookie_consent.cookieName')}
    consentCookieLifetime = ${Configuration.setting('Wysiwyg.CookieHandling.cookieGroups.technical.cookies.cookie_consent.lifetime')}
    cookieConsentAcceptedName = ${Configuration.setting('Wysiwyg.CookieHandling.cookieGroups.technical.cookies.cookie_accepted.cookieName')}
    consentCookieAcceptedLifetime = ${Configuration.setting('Wysiwyg.CookieHandling.cookieGroups.technical.cookies.cookie_accepted.lifetime')}

    // Translations
    translationPackage = 'Wysiwyg.CookieHandling'
    translationLocale = null

    // Uris
    privacyStatementPageUri = ''
    legalNodesPageUri = ''

    // Display Settings
    cookieLayerDisabled = 'false'
    showCookieDetails = ${Configuration.setting('Wysiwyg.CookieHandling.showCookieDetails')}

    renderer = Wysiwyg.CookieHandling:Component.Organism.Modal {
        id = ${props.modalId}
        header = Wysiwyg.CookieHandling:Component.Molecule.ModalHeader {
            title = ${I18n.translate('modalTitle' , null, [], 'CookieLayer', props.translationPackage, null, props.translationLocale)}
        }

        body = Wysiwyg.CookieHandling:Component.Molecule.CookieLayerBody {
            privacyAnchor = Wysiwyg.CookieHandling:Component.Atom.Anchor {
                uri = ${props.privacyStatementPageUri}
                text = ${I18n.translate('privacyLinkText' , null, [], 'CookieLayer', props.translationPackage, null, props.translationLocale)}
            }

            legalNotesAnchor = Wysiwyg.CookieHandling:Component.Atom.Anchor {
                uri = ${props.legalNodesPageUri}
                text = ${I18n.translate('legalNotesLinkText' , null, [], 'CookieLayer', props.translationPackage, null, props.translationLocale)}
            }

            introTextTranslated = ${I18n.translate('cookieConsentText' , null, [], 'CookieLayer', props.translationPackage, null, props.translationLocale)}
            introText = ${String.replace(this.introTextTranslated, ['{privacyStatementAnchor}', '{legalNotesAnchor}'], [this.privacyAnchor, this.legalNotesAnchor])}

            cookieLayerDisabled = ${props.cookieLayerDisabled}
            consentCookieName = ${props.consentCookieName}
            consentCookieLifetime = ${props.consentCookieLifetime}
            consentCookieAcceptedName = ${props.cookieConsentAcceptedName}
            consentCookieAcceptedLifetime = ${props.consentCookieAcceptedLifetime}

            cookieBoxes = Wysiwyg.CookieHandling:Component.Molecule.CookieBoxes {
                translationPackage = ${props.translationPackage}
                translationLocale = ${props.translationLocale}
            }
        }

        footer = Wysiwyg.CookieHandling:Component.Molecule.CookieLayerFooter {
            saveButton = Wysiwyg.CookieHandling:Component.Atom.Button {
                id = 'saveCookies'
                class = 'btn btn-secondary btn-lg'
                text = ${I18n.translate('confirmSelectionButtonText', null, [], 'CookieLayer', props.translationPackage, null, props.translationLocale)}
            }
            saveAllButton = Wysiwyg.CookieHandling:Component.Atom.Button {
                id = 'saveAllCookies'
                class = 'btn btn-primary btn-lg'
                text = ${I18n.translate('selectAllButtonText', null, [], 'CookieLayer', props.translationPackage, null, props.translationLocale)}
            }
        }
    }
}
